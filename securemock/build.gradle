plugins {
    id "com.github.johnrengelman.shadow" version "2.0.2"
}

apply plugin: 'java'

dependencies {
    runtime project(':securitymanager-mockito')
    runtime "org.objenesis:objenesis:${project.property('objenesis.version')}"
    runtime "net.bytebuddy:byte-buddy:${project.property('bytebuddy.version')}"
    runtime "org.ow2.asm:asm:${project.property('asm.version')}"
}

javadoc {
    // we have no code to document, skip javadoc to prevent failure of task
    enabled = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, shadowJar
}

shadowJar {
    // can not set classifier to null because of publishing:
    // "A POM cannot have multiple artifacts with the same type and classifier."
    //baseName = "${project.archivesBaseName}-shadow"
    classifier = 'shadow'
    artifacts {
        shadow(tasks.shadowJar.archivePath) {
            builtBy shadowJar
        }
    }
    // relocate so other projects can include objenesis/bytebuddy/asm as dependency
    relocate('org.objenesis', 'org.xbib.securemock.objenesis')
    relocate('net.bytebuddy', 'org.xbib.securemock.bytebuddy')
    relocate('org.objectweb.asm', 'org.xbib.securemock.asm')
}
