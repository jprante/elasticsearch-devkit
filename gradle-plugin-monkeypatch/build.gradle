plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
}

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

jar {
    manifest {
        attributes('Implementation-Version': project.version)
    }
}

test {
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

groovydoc {
    docTitle = description
    groovyClasspath = tasks.jar.outputs.files
    groovyClasspath += configurations.compile
    groovyClasspath += configurations.runtime
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar, groovydocJar
}

if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = 'https://github.com/jprante/elasticsearch-xbib-devkit/gradle-plugin-monkeypatch'
        vcsUrl = 'https://github.com/jprante/elasticsearch-xbib-devkit/gradle-plugin-monkeypatch'
        plugins {
            monkeyPatchPlugin {
                id = 'org.xbib.gradle.plugin.monkeypatch'
                description = 'Gradle plugin for monkey patching'
                displayName = 'Gradle plugin for monkey patching'
                tags = ['xbib', 'patch', 'monkeypatch']
                version = project.version
            }
        }
    }
}
