buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://xbib.org/repository'
        }
    }
    dependencies {
        classpath "org.xbib.elasticsearch:gradle-plugin-elasticsearch-build:6.3.2.4"
    }
}

apply plugin: 'org.xbib.gradle.plugin.elasticsearch.plugin'
apply plugin: 'signing'

configurations {
    wagon
}

dependencies {
    testCompile project(':elasticsearch-test-framework')
    testCompile "org.xbib.elasticsearch:elasticsearch:${project.property('elasticsearch-server.version')}"
    wagon "org.apache.maven.wagon:wagon-ssh:${project.property('wagon.version')}"
}

afterEvaluate {
    compileJava {
        inputs.property("moduleName", 'org.xbib.elasticsearch.analysis.common')
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath
            ]
            classpath = files()
        }
    }
    compileTestJava {
        inputs.property("moduleName", 'org.xbib.elasticsearch.analysis.common.test')
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath
            ]
            classpath = files()
        }
    }
    javadoc {
        inputs.property("moduleName", 'org.xbib.elasticsearch.analysis.common')
        doFirst {
            options.addStringOption('-module-path', classpath.asPath)
            classpath = files()
        }
    }
}

clean {
    delete fileTree('.') { include '.local*.log' }
}

esplugin {
    description 'Common analyzers for Elasticsearch'
    classname 'org.elasticsearch.analysis.common.CommonAnalysisPlugin'
    url 'https://github.com/jprante/elasticsearch-devkit/elasticsearch-analysis-common'
}

dependencyLicenses.enabled = false

// do not run junit tests
test.enabled = false

// do not run randomized tests
randomizedTest.enabled = false

// run Elasticsearch tests
esTest {
    enabled = true
    systemProperty 'tests.logger.level', 'ALL'
    systemProperty 'tests.security.manager', 'true'
    //systemProperty 'java.security.debug', 'access,failure'
}

integTest {
    enabled = false // 0 Tests
}

configurations {
    archives
}

artifacts {
    archives jar, testJar, sourcesJar, javadocJar, bundlePlugin
}

signing {
    sign bundlePlugin
    sign testJar
}

apply from: "${rootProject.projectDir}/gradle/publish.gradle"
